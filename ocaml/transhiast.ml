open Hifirrtl_lang
open Printf
open Extraction
open Big_int_Z

module StringMap = Map.Make(String)
let initmap_s = StringMap.empty
module IntMap = Map.Make(Stdlib.Int)
let initmap_i = IntMap.empty

module IntPair = struct
  type t = int * int
  let compare = Stdlib.compare
end
 
module IntPairMap = Map.Make(IntPair)

let rec mapbtyp_helper v (map, flag, flag') ft = 
  match ft with
  | Ast.Gtyp _ -> (StringMap.add v (flag' :: flag) map, flag' + 1)
  | Ast.Atyp (atyp, _) -> mapbtyp_helper v (map, flag, flag') atyp
  | Ast.Btyp btyp -> let map0 = StringMap.add v (flag' :: flag) map in
                     let (map1, _, flag'0) = mapbtyp v (map0, (flag' :: flag), 0) btyp in (map1, flag' + 1)
  and mapbtyp v (map, flag, flag') btyp =
  match btyp with 
  | Ast.Fnil -> (map, flag, flag')
  | Ast.Fflips (fv, _, ft, ff) -> let map0 = StringMap.add (v^"."^fv) (flag' :: flag) map in
                                  let (map1, flag'0) = mapbtyp_helper (v^"."^fv) (map0, flag, flag') ft in
                                  mapbtyp v (map1, flag, flag'0) ff

let rec mapftype v (map, flag) ft = 
match ft with
| Ast.Gtyp _ -> ((StringMap.add v [flag] map), flag+1)
| Ast.Atyp (atyp, _) -> mapftype v (map, flag) atyp
| Ast.Btyp btyp -> let (map0, _, _) = mapbtyp v (StringMap.add v [flag] map, [flag], 0) btyp in
                   (map0, flag + 1)
  
let mapport (((map, map_i), flag), tmap) p = 
  match p with
  | Ast.Finput (v, ty) -> let (map0, flag0) = mapftype v (map, flag) ty in 
                          (((map0, IntMap.add flag v map_i), flag0), StringMap.add v ty tmap)
  | Ast.Foutput (v, ty) -> let (map0, flag0) = mapftype v (map, flag) ty in 
                          (((map0, IntMap.add flag v map_i), flag0), StringMap.add v ty tmap)

let rec mapstmt (((map, map_i), flag), tmap) stmt = 
  match stmt with
  | Ast.Swire (v, ty) -> let (map0, flag0) = mapftype v (map, flag) ty in 
                          (((map0, IntMap.add flag v map_i), flag0), StringMap.add v ty tmap)
  | Ast.Sreg (v, r) -> let (map0, flag0) = mapftype v (map, flag) r.coq_type in 
                          (((map0, IntMap.add flag v map_i), flag0), StringMap.add v r.coq_type tmap)
  | Ast.Smem (v, ty, n) -> let (map0, flag0) = mapftype v (map, flag) (Ast.Atyp (ty, n)) in 
                        (((map0, IntMap.add flag v map_i), flag0), StringMap.add v (Ast.Atyp (ty, n)) tmap)
  | Ast.Snode (v, e) -> (match Nodehelper.type_of_hfexpr e tmap with
                      | Some ty -> let (map0, flag0) = mapftype v (map, flag) ty in 
                          (((map0, IntMap.add flag v map_i), flag0), StringMap.add v ty tmap)
                      | None -> printf "%s wrong expr type\n" v; Ast.pp_expr stdout e; (((map, map_i), flag), tmap))
  | Ast.Sinst (v, _) -> (((StringMap.add v [flag] map, IntMap.add flag v map_i), flag + 1), tmap)
  | Ast.Sinferport (v, r, _) -> (match Nodehelper.type_of_ref r tmap with
                      | Some ty -> let (map0, flag0) = mapftype v (map, flag) ty in 
                          (((map0, IntMap.add flag v map_i), flag0), StringMap.add v ty tmap)
                      | None -> (((map, map_i), flag), tmap))
  | Ast.Swhen (_, s1, s2) -> mapstmts (mapstmts (((map, map_i), flag), tmap) s1) s2
  | _ -> (((map, map_i),flag), tmap)

 and mapstmts (((map, map_i), flag), tmap) stmts = 
  match stmts with
  | Ast.Qnil -> (((map, map_i), flag), tmap)
  | Ast.Qcons (s, ss) -> mapstmts (mapstmt (((map, map_i), flag), tmap) s) ss

let mapmod (((map, map_i), flag), tmap) m =  
  match m with 
  | Ast.FInmod (_, pl, sl) -> let (((map0, map0_i), flag0), tmap0) = Stdlib.List.fold_left mapport (((map, map_i), flag), tmap) pl in
                              mapstmts (((map0, map0_i), flag0),tmap0) sl 
  | _ -> (((map, map_i), flag), tmap)

let mapcir cir = 
  match cir with
  | Ast.Fcircuit (_, ml) -> mapmod (((initmap_s, initmap_i), 0), initmap_s) (Stdlib.List.hd ml)

(*transast*)

let trans_fgtyp ty = 
  match ty with
  | Ast.Fuint s -> Env.Fuint s
  | Ast.Fsint s -> Env.Fsint s
  | Ast.Fuint_implicit s -> Env.Fuint s
  | Ast.Fsint_implicit s -> Env.Fsint s
  | Ast.Fclock -> Env.Fclock
  | Ast.Freset -> Env.Freset
  | Ast.Fasyncreset -> Env.Fasyncreset

let trans_flip f = 
  match f with
  | Ast.Flipped -> HiEnv.Flipped
  | Ast.Nflip -> HiEnv.Nflip

let trans_ucast a_ucast = 
  match a_ucast with
  | Ast.AsUInt -> LoFirrtl.AsUInt
  | Ast.AsSInt -> LoFirrtl.AsSInt
  | Ast.AsClock -> LoFirrtl.AsClock
  | Ast.AsAsync -> LoFirrtl.AsAsync

let trans_eunop a_eunop = 
  match a_eunop with
  | Ast.Upad s -> LoFirrtl.Upad s
  | Ast.Ushl s -> LoFirrtl.Ushl s
  | Ast.Ushr s -> LoFirrtl.Ushr s
  | Ast.Ucvt -> LoFirrtl.Ucvt
  | Ast.Uneg -> LoFirrtl.Uneg
  | Ast.Unot -> LoFirrtl.Unot
  | Ast.Uandr -> LoFirrtl.Uandr
  | Ast.Uorr -> LoFirrtl.Uorr
  | Ast.Uxorr -> LoFirrtl.Uxorr
  | Ast.Uextr (s1, s2) -> LoFirrtl.Uextr (s1, s2)
  | Ast.Uhead s -> LoFirrtl.Uhead s
  | Ast.Utail s -> LoFirrtl.Utail s

let trans_cmp a_cmp = 
  match a_cmp with
  | Ast.Blt -> LoFirrtl.Blt
  | Ast.Bleq -> LoFirrtl.Bleq
  | Ast.Bgt -> LoFirrtl.Bgt
  | Ast.Bgeq -> LoFirrtl.Bgeq
  | Ast.Beq -> LoFirrtl.Beq
  | Ast.Bneq -> LoFirrtl.Bneq

let trans_ebinop a_ebinop = 
  match a_ebinop with
  | Ast.Badd -> LoFirrtl.Badd
  | Ast.Bsub -> LoFirrtl.Bsub
  | Ast.Bmul -> LoFirrtl.Bmul
  | Ast.Bdiv -> LoFirrtl.Bdiv
  | Ast.Brem -> LoFirrtl.Brem
  | Ast.Bcomp s -> LoFirrtl.Bcomp (trans_cmp s)
  | Ast.Bdshl -> LoFirrtl.Bdshl
  | Ast.Bdshr -> LoFirrtl.Bdshr
  | Ast.Band -> LoFirrtl.Band
  | Ast.Bor -> LoFirrtl.Bor
  | Ast.Bxor -> LoFirrtl.Bxor
  | Ast.Bcat -> LoFirrtl.Bcat

let binary_length (signed : bool) (n: Z.t) : int =
  if n = Z.zero then 1 
  else let bits = Z.numbits (Z.abs n) in
    if signed then
      if n > Z.zero then
        bits + 1
      else if (Z.popcount (Z.abs n)) = 1 then bits
        else bits + 1
    else bits

let rec int_to_bool_list n bits_remaining =
  if bits_remaining = 0 then []
  else
    let bit = Z.testbit n (bits_remaining - 1) in
    bit :: int_to_bool_list n (bits_remaining - 1)

let bits_of_z (n: Z.t) (bit_length: int) : bool list =
  let two_power_bit_length = Z.shift_left Z.one bit_length in
  let twos_complement =
    if n >= Z.zero then
      Z.rem n two_power_bit_length
    else
      Z.add two_power_bit_length (Z.rem n two_power_bit_length)
  in
  int_to_bool_list twos_complement bit_length

let rec trans_ftype v ty map = 
  match ty with
  | Ast.Gtyp gt -> HiEnv.Gtyp (trans_fgtyp gt)
  | Ast.Atyp (atyp, n) -> HiEnv.Atyp (trans_ftype v atyp map, n)
  | Ast.Btyp btyp -> HiEnv.Btyp (trans_btyp v btyp map)

and trans_btyp v btyp map =
  match btyp with
  | Ast.Fnil -> HiEnv.Fnil
  | Ast.Fflips (fv, fl, ft, ff) -> HiEnv.Fflips (Obj.magic (Stdlib.List.hd (StringMap.find (v^"."^fv) map)), trans_flip fl, trans_ftype (v^"."^fv) ft map, (trans_btyp v ff map))

let rec find_nat4v ref =
  match ref with
  | Ast.Eid v -> v
  | Ast.Esubfield (ref1, v) -> (find_nat4v ref1)^"."^v
  | Ast.Esubindex (ref1, _) -> (find_nat4v ref1) 
  | Ast.Esubaccess (ref1, _) -> (find_nat4v ref1)

let rec trans_ref ref map = 
  match ref with
  | Ast.Eid v -> HiFirrtl.Eid (Obj.magic (Stdlib.List.hd (StringMap.find v map)))
  | Ast.Esubfield (r, _) -> HiFirrtl.Esubfield (trans_ref r map, Obj.magic (Stdlib.List.hd (StringMap.find (find_nat4v ref) map)))
  | Ast.Esubindex (r, n) -> HiFirrtl.Esubindex (trans_ref r map, n)
  | Ast.Esubaccess (r, e) -> HiFirrtl.Esubaccess (trans_ref r map, trans_expr e map)

and trans_expr e map = 
  match e with
  | Ast.Econst (ty, s) -> (match ty with
    | Ast.Fuint_implicit _ -> HiFirrtl.Econst (trans_fgtyp (Ast.Fuint (binary_length false s)), bits_of_z s (binary_length false s))
    | Ast.Fsint_implicit _ -> HiFirrtl.Econst (trans_fgtyp (Ast.Fsint (binary_length true s)), bits_of_z s (binary_length true s))
    | _ -> HiFirrtl.Econst (trans_fgtyp ty, bits_of_z s (Env.sizeof_fgtyp (trans_fgtyp ty))))
  | Ast.Eref v -> HiFirrtl.Eref (trans_ref v map)
  | Ast.Eprim_unop (op, e1) -> HiFirrtl.Eprim_unop(trans_eunop op, trans_expr e1 map)
  | Ast.Eprim_binop (bop, e1, e2) -> HiFirrtl.Eprim_binop(trans_ebinop bop, trans_expr e1 map, trans_expr e2 map)
  | Ast.Emux (e1,e2,e3) -> HiFirrtl.Emux(trans_expr e1 map, trans_expr e2 map, trans_expr e3 map)
  | Ast.Ecast (s, e) -> HiFirrtl.Ecast(trans_ucast s, trans_expr e map)
  
let trans_ruw r = 
  match r with
  | Ast.Coq_old -> LoFirrtl.Coq_old
  | Ast.Coq_new -> LoFirrtl.Coq_new
  | Ast.Coq_undefined -> LoFirrtl.Coq_undefined

let trans_rst rst map = 
  match rst with
  | Ast.NRst -> HiFirrtl.NRst
  | Ast.Rst (e1, e2) -> HiFirrtl.Rst(trans_expr e1 map, trans_expr e2 map)

let mk_freg t c r = { HiFirrtl.coq_type = t; clock = c; reset = r}

let rec trans_stmt s map res tmap = 
  match s with
  | Ast.Sskip -> HiFirrtl.Qcons (HiFirrtl.Sskip, res)
  | Ast.Swire (v, ty) -> 
                let ns = HiFirrtl.Swire(Obj.magic (Stdlib.List.hd (StringMap.find v map)), trans_ftype v ty map) in
                HiFirrtl.Qcons (ns, res)
  | Ast.Sfcnct (ref, e) -> let ns = HiFirrtl.Sfcnct((trans_ref ref map), trans_expr e map) in
                HiFirrtl.Qcons (ns, res)
  | Ast.Sinvalid ref -> let ns = HiFirrtl.Sinvalid ((trans_ref ref map)) in
                HiFirrtl.Qcons (ns, res)
  | Ast.Snode (v, e) -> 
                let ns = HiFirrtl.Snode(Obj.magic (Stdlib.List.hd (StringMap.find v map)), trans_expr e map) in
                HiFirrtl.Qcons (ns, res)
  | Ast.Sreg (v, r) -> 
                let ns = HiFirrtl.Sreg (Obj.magic (Stdlib.List.hd (StringMap.find v map)), mk_freg (trans_ftype v r.coq_type map) (trans_expr r.clock map) (trans_rst r.reset map)) in
                HiFirrtl.Qcons (ns, res)
  | Ast.Swhen (c, s1, s2) -> let ns = HiFirrtl.Swhen (trans_expr c map, trans_stmts s1 map HiFirrtl.Qnil tmap, trans_stmts s2 map HiFirrtl.Qnil tmap) in
                    HiFirrtl.Qcons (ns, res)
  | _ -> res


and trans_stmts ss map res tmap =
  match ss with
  | Ast.Qnil -> res
  | Ast.Qcons (s, st) -> trans_stmts st map (trans_stmt s map res tmap) tmap

let trans_port p map = 
  match p with
  | Ast.Finput (v, ty) -> HiFirrtl.Finput(Obj.magic (Stdlib.List.hd (StringMap.find v map)), trans_ftype v ty map)
  | Ast.Foutput (v, ty) -> HiFirrtl.Foutput(Obj.magic (Stdlib.List.hd (StringMap.find v map)), trans_ftype v ty map)

let rec revstmts sts res = 
  match sts with 
  | HiFirrtl.Qnil -> res
  | Qcons (h, tl) -> revstmts tl (revstmt h res)
    
and revstmt st res =
  match st with
  | HiFirrtl.Swhen (c, s1, s2) -> HiFirrtl.Qcons ((HiFirrtl.Swhen (c, revstmts s1 HiFirrtl.Qnil, revstmts s2 HiFirrtl.Qnil)), res)
  | _ -> Qcons (st, res)

let trans_mod m map flag tmap = 
  match m with
  | Ast.FInmod (_, pl, sl) -> let newstmts = trans_stmts sl map HiFirrtl.Qnil tmap in
                              HiFirrtl.FInmod(Obj.magic flag, List.map (fun a -> trans_port a map) pl, revstmts newstmts HiFirrtl.Qnil)
  | Ast.FExmod (_, pl, sl) -> let newstmts = trans_stmts sl map HiFirrtl.Qnil tmap in
                              HiFirrtl.FExmod(Obj.magic flag, List.map (fun a -> trans_port a map) pl, revstmts newstmts HiFirrtl.Qnil)

let trans_cir cir map flag tmap = 
  match cir with
  | Ast.Fcircuit (_, ml) -> HiFirrtl.Fcircuit ((Obj.magic flag), (trans_mod (Stdlib.List.hd ml) map (flag + 1) tmap) :: [])

let process_string str suffix =
  let len = String.length str in
  if len >= 4 && String.sub str (len - 4) 4 = ".fir" then
    String.sub str 0 (len - 4) ^ suffix
  else
    str 

let hiparse f =
  let lexbuf = Lexing.from_channel (open_in f) in
  FirrtlParser.file FirrtlLexer.token lexbuf
